
Reverb <1, 2>: {
    LBCF <1, 1>: {
        ; Modulated Schroeder-Moorer lowpass feedback comb filter
        ; https://ccrma.stanford.edu/~jos/pasp/Lowpass_Feedback_Comb_Filter.html
    
        N: _1   ; Filter order
        f: _2   ; Feedback
        d: 0.3 ; Damping
    
        input
        -> sum: Add~
        -> buffer: Delay_Line~ N
        -> * -f
        -> Real_Pole~ d
        -> 1|sum
    
        -> output
    
        ; Delay_Modulator~ N, random(0.1), 0.1
        ; -> 1|buffer
    }

    offset: _1
    
    ; From freeverb,
    orders: { 1557, 1617, 1491, 1422, 1277, 1356, 1188, 1113 } + offset

    ; Eight parallel combs create a sufficient echo density
    input <> [8] LBCF~ orders[n-1], _2
    >> * 0.1 -> output
}



Compressor <1, 1>: {
    ; The compression is quite strong just to make it clear
    ratio: _1
    threshold: _2

    slope: 1 - 1 / ratio


    Max_Zero <1, 1>: {
        ; Essentially a negative half-wave rectifier.
    
        input -> Comparator~ 0 -> Inverse~ -> +1 -> 1|gate: Multiply~
        input -> gate -> output
    }


    input -> Envelope_Follower~ 4ms, 20ms
    -> Linear_to_Decibel~
    -> Inverse~ -> +threshold -> *slope -> Max_Zero~
    -> Decibel_to_Linear~
    -> 1|gain: Multiply~

    input -> gain -> output
}
