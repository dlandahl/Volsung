
; Modulated Schroeder-Moorer lowpass feedback comb filter
LBCF <1, 1>: {
    M: _1 ; Filter order

    input
    -> sum: Add~
    -> buffer: Delay_Line~ M
    -> *-0.84         ; Decay
    -> Smooth~ 15000  ; Colour
    -> 1|sum

	-> output

    mod_scale: 0.2
    Sine_Oscillator~ random(0.1)
    -> Bi_to_Unipolar~
    -> Multiply~ M * mod_scale
    -> Add~  M - M * mod_scale
    -> 1|buffer
}

; From Freeverb
orders: { 1557, 1617, 1491, 1422, 1277, 1356, 1188, 1113 }

; Eight parallel combs
File~ "files/drumbeat"
<> [8] LBCF~ orders[n-1]
>> * 0.1 -> output

