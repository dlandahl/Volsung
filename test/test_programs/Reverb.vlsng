
; Try listening without the reverb, to understand the
; effect the program has on the raw input
enable_reverb: true

Delay_Modulator <0, 1>: {
    ; A subgraph that modulates a delay buffer relative to its length

    size: _1
    rate: _2
    mod_depth: _3

    Sine_Oscillator~ rate, random(tau)
    -> Bi_to_Unipolar~
    -> Multiply~    size * mod_depth
    -> Add~  size - size * mod_depth
    -> output
}

One_Pole <1, 1>: {
    ; A filter with a single pole on the real axis.
    ; Used as a damping filter in the reverb feedback loop, to simulate
    ; higher frequency components dissipating more quickly

    a: _1
    input -> *1-a -> Pole~ a -> output
}

LBCF <1, 1>: {
    ; Modulated Schroeder-Moorer lowpass feedback comb filter
    ; https://ccrma.stanford.edu/~jos/pasp/Lowpass_Feedback_Comb_Filter.html

    N: _1   ; Filter order
    f: _2   ; Feedback
    d: 0.22 ; Damping

    input
    -> sum: Add~
    -> buffer: Delay_Line~ N
    -> * -f
    -> One_Pole~ d
    -> 1|sum

    -> output

    Delay_Modulator~ N, random(0.1), 0.1
    -> 1|buffer
}

Reverb <1, 2>: {
    offset: _1
    
    ; From freeverb,
    orders: { 1557, 1617, 1491, 1422, 1277, 1356, 1188, 1113 } + offset

    ; Eight parallel combs create a sufficient echo density
    input <> [8] LBCF~ orders[n-1], _2
    >> * 0.1 -> output
}

Switch <1, 2>: {
    input <> bypass: [2] Multiply~ _1 - (n-1)
    bypass[0] -> 0|output
    bypass[1] -> 1|output
}

decay: 0.89

; Also try `files/drumbeat`
path: "files/antikythera"

Read_File~ path
-> bypass: Switch~ enable_reverb
<> reverb: [2] Reverb~ 20*(n-1), decay

reverb[0] -> 0|output
reverb[1] -> 1|output
bypass|1 -> 0|output
bypass|1 -> 1|output

&length length_of(read_file(path)) + 1s
